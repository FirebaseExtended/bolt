// Mail.bolt - Inbox and Outbox Rules.
//
// These rules support putting Messages in an Inbox (for another user)
// and making a record of all email sent in an outbox.
//
// Design goals:
//
// - Anyone can send a message to anybody by posting in the users inbox, as long as they have
//   the correct from field.
// - Cannot forge the from field of a message.
// - Cannot alter a message once created (it can only be deleted in-toto).
//
// Functionally similar rules to the corresponding rules defined in the Blaze Compiler:
// https://github.com/firebase/blaze_compiler/blob/master/examples/mail_example.yaml

type Message {
  from: String,
  to: String,
  message: String,

  validate() {
    return isNew(this) && isCurrentUser(this.from);
  }
}

// Inbox
path /users/$userid/inbox/$msg is Message {
  read() = isCurrentUser($userid);
  write() = isNew(this) || isCurrentUser($userid);
}

// Outbox
path /users/$userid/outbox/$msg is Message {
  read() = isCurrentUser($userid);
  write() = isCurrentUser($userid);
}

isNew(ref) = prior(ref) == null;

isCurrentUser(userid) = currentUser() == userid;

// TODO: Use auth.uid - so not require customer token.
currentUser() = auth.username;
